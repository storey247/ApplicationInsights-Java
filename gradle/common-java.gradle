/*
 * ApplicationInsights-Java
 * Copyright (c) Microsoft Corporation
 * All rights reserved.
 *
 * MIT License
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this
 * software and associated documentation files (the ""Software""), to deal in the Software
 * without restriction, including without limitation the rights to use, copy, modify, merge,
 * publish, distribute, sublicense, and/or sell copies of the Software, and to permit
 * persons to whom the Software is furnished to do so, subject to the following conditions:
 * The above copyright notice and this permission notice shall be included in all copies or
 * substantial portions of the Software.
 * THE SOFTWARE IS PROVIDED *AS IS*, WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 * INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR
 * PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE
 * FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR
 * OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
 * DEALINGS IN THE SOFTWARE.
 */

// region Shared java configuration

import org.gradle.internal.os.OperatingSystem;
import org.gradle.api.tasks.testing.logging.TestLogEvent;
import org.gradle.api.tasks.testing.logging.TestExceptionFormat;

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'org.owasp.dependencycheck'

sourceCompatibility = 1.7
targetCompatibility = 1.7

checkstyle {
    toolVersion = '8.22'
    configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
    configProperties["rootDir"] = rootProject.projectDir
    showViolations = false
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled true
        html.enabled !isBuildServer
    }
}

dependencyCheck {
    def supFile = "${project.projectDir}/dependency-check-suppressions.xml"
    if (file(supFile).exists() && !Project.hasProperty('dependencyCheck.suppressions.skip')) {
        suppressionFiles += supFile
    }
    outputDirectory = "${project.buildDir}/reports/dependency-check"
    formats = ['HTML', 'JUNIT']
    skipConfigurations = ['mavenDeployer', 'findbugs', 'checkstyle']
    cveValidForHours = 1
    failBuildOnCVSS = 0
}

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

if (hasProperty("JDKToUse")) {
    logger.info("using $JDKToUse to compile and run tests")
    def javaExecutablesPath = new File(JDKToUse, 'bin')
    def javaExecutables = [:].withDefault { execName ->
        def execNameToUse = execName
        if (OperatingSystem.current().isWindows()){
            execNameToUse = execName + ".exe"
        }
        def executable = new File(javaExecutablesPath, execNameToUse)
        assert executable.exists(): "There is no ${execName} executable in ${javaExecutablesPath}"
        executable
    }
    tasks.withType(AbstractCompile) {
        options.with {
            fork = true
            forkOptions.executable = javaExecutables.javac
            forkOptions.javaHome = JDKToUse
        }
    }
    tasks.withType(Javadoc) {
        executable = javaExecutables.javadoc
    }
    tasks.withType(Test) {
        executable = javaExecutables.java
    }
    tasks.withType(JavaExec) {
        executable = javaExecutables.java
    }
} else {
    logger.info("JDKToUse not found. Using default JAVA_HOME")
}
updateTestDirectories()

// setting this variable is highly recommended
def java7JreDir = System.env.'JAVA_JRE_7'
def isJava7 = JavaVersion.current().isJava7()
def isJava7Compatible = JavaVersion.current().isJava7Compatible()

if (!isJava7Compatible) {
    logger.error "The current JDK version isn't compatible with Java 7: "+JavaVersion.current()+". Please upgrade JDK 1.7+"
    if (isBuildServer || isRelease) {
        throw new GradleException("incompatible JDK version: ${JavaVersion.current()}; needs 1.7+")
    }
}
if (java7JreDir) {
    def jre7libDir = fileTree(dir: "$java7JreDir/lib", include: '*.jar')
    if (!jre7libDir.any()) {
        throw new GradleException("Invalid JAVA_JRE_7 directory. ${jre7libDir.dir} was empty")
    }

    tasks.withType(JavaCompile) {
        options.bootstrapClasspath = jre7libDir
    }
} else {
    def msg = "IMPORTANT: Environment variable 'JAVA_JRE_7' is not defined - Install JRE 7 and set 'JAVA_JRE_7' to prevent runtime compatibility issues!"
    if (!isJava7) {
        if (isBuildServer || isRelease) {
            logger.error msg
            throw new GradleException("JAVA_JRE_7 must be set")
        } else {
            logger.warn msg
        }
    }
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integTest/java')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

task integrationTest(type: Test, dependsOn: jar) {
    group 'Verification'
    description 'Runs the integration tests.'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

repositories {
    mavenCentral()
}

task collectTestResults {
    doLast {
        copy {
            from testResultsDir
            into new File(rootProjectBuildDir, "$remoteTestResultsDirName/$project.name")
        }
        copy {
            from testReportDir
            into new File(rootProjectBuildDir, "$remoteTestReportDirName/$project.name")
        }
    }
}

task testAndCollect {
    dependsOn test
    finalizedBy collectTestResults
}

task vscodeClasspathFile {
    description 'Generates classpath file for the Visual Studio Code java plugin'
    ext.destFile = file("$buildDir/classpath.txt")
    outputs.file destFile
    doLast {
        def classpathString = configurations.compile.collect{ it.absolutePath }.join(File.pathSeparator)
        if (!destFile.parentFile.exists()) {
            destFile.parentFile.mkdirs()
        }
        assert destFile.parentFile.exists()
        destFile.text = classpathString
    }
}

task vscodeJavaconfigFile(dependsOn: vscodeClasspathFile) {
    description 'Generates javaconfig.json file for the Visual Studio Code java plugin'

    def relativePath = { File f ->
        f.absolutePath - "${project.rootDir.absolutePath}/"
    }
    ext.destFile = file("javaconfig.json")
    ext.config = [
        sourcePath: sourceSets.collect{ it.java.srcDirs }.flatten().collect{ relativePath(it) },
        classPathFile: relativePath(tasks.getByPath('vscodeClasspathFile').outputs.files.singleFile),
        outputDirectory: relativePath(new File(buildDir, 'vscode-classes'))
    ]
    doLast {
        def jsonContent = groovy.json.JsonOutput.toJson(ext.config)
        destFile.text = groovy.json.JsonOutput.prettyPrint(jsonContent)
    }
}

task vscode(dependsOn: vscodeJavaconfigFile) {
    description 'Generates config files for the Visual Studio Code java plugin'
    group 'vscode'
}

tasks.withType(Test) {
    testLogging {
        afterSuite { desc, result ->
            if (!desc.parent) { // only print when the top-level test suite completes
                logger.lifecycle("${desc.name} complete. Ran ${result.testCount} tests. ${result.failedTestCount} failed, ${result.skippedTestCount} skipped.")
            }
        }

        info {
            events  TestLogEvent.FAILED,
                    TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED,
                    TestLogEvent.STANDARD_ERROR

            exceptionFormat TestExceptionFormat.FULL
            showExceptions true
            showCauses true
            showStackTraces true
        }

        debug {
            events = info.events
            events << TestLogEvent.STARTED
            events << TestLogEvent.STANDARD_OUT

            exceptionFormat = info.exceptionFormat
            showExceptions = info.showExceptions
            showCauses = info.showCauses
            showStackTraces = info.showStackTraces
        }
    }
}


// endregion Shared java configuration

// region Helper Methods

def updateTestDirectories() {
    if (this.hasProperty("testEnvName")) {
        testResultsDirName = testResultsDirName + "/" + testEnvName
        logger.info('Test results folder for project "' + project.name + '" set to "' + testResultsDir + '"')

        testReportDirName = testReportDirName + "/" + testEnvName
        logger.info('Test report folder for project "' + project.name + '" set to ' + testReportDirName + '"')
    }
}

// endregion Helper Methods
